# Maintainer:  Joost Bremmer <toost.b@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Danilo Bargen <gezuru@gmail.com>

pkgname=neovim-jedi
_reponame=jedi-vim
pkgver=0.8.0
pkgrel=1
pkgdesc='Neovim plugin for jedi, an awesome Python autocompletion.'
arch=('any')
url="https://github.com/davidhalter/${_reponame}"
license=('MIT')
depends=('neovim' 'python-jedi' 'python')
optdepends=('neovim-supertab: for tab completion'
            'python2-jedi: for Python 2 support')
groups=('neovim-plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/davidhalter/${_reponame}/archive/${pkgver}.tar.gz")
sha256sums=('d1f000b8c93c915a1bcc379a4784e68e58192747a6fddc992af04245f68743b6')
install=$pkgname.install

package() {
  cd "$srcdir/$_reponame-$pkgver"

  local _installdir="usr/share/nvim/runtime/"
  install -d "${pkgdir}/$_installdir/doc"

  cp -dp --no-preserve=ownership jedi_vim.py initialize.py "${pkgdir}/$_installdir/"
  cp -dp --no-preserve=ownership doc/jedi-vim.txt "${pkgdir}/$_installdir/doc/"
  cp -dpr --no-preserve=ownership after "${pkgdir}/$_installdir/after/"
  cp -dpr --no-preserve=ownership autoload "${pkgdir}/$_installdir/autoload"
  cp -dpr --no-preserve=ownership ftplugin "${pkgdir}/$_installdir/ftplugin"
  cp -dpr --no-preserve=ownership plugin "${pkgdir}/$_installdir/plugin"

  # install license file
  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
